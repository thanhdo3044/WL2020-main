/*
DECLARE @E INT
EXEC Proc_createStepOfPallet
'19322',
100001,
100002,
20,
0,
100000,
100000,
@ERROR = @E

*/
/*
EXEC Proc_createStepOfPallet
'100000',
100001,
100002,
100001
*/

CREATE OR ALTER PROC Proc_movePallet -- dùng sau khi lựa phôi
@PALLET_CODE VARCHAR(20),
@FROM_ID INT,
@TO_ID INT,
@KILN_BATCH_ID INT,
@PASS MONEY,
@IKEA_GUID VARCHAR(100),
@PLAN_ID INT,
@VENDOR_ID INT,
@CHEMISTRY_ID INT,
@PARCEL_ID INT,
@ACCOUNT_ID INT,
@ERROR INT OUTPUT
AS
BEGIN
 -- kiểm tra mã ikeaGuid
 IF(@IKEA_GUID IS NOT NULL)
	BEGIN
		IF EXISTS (	SELECT PL.ID
					FROM prod.PALLET PL
					WHERE PL.IKEA_GUID = @IKEA_GUID
					)
			BEGIN
				SELECT @PALLET_CODE = PL.CODE, @PASS = IIP.QUANTITY
				FROM prod.PALLET PL
				LEFT JOIN prod.ITEM_IN_PALLET IIP ON IIP.PALLET_ID = PL.ID
				WHERE PL.IKEA_GUID = @IKEA_GUID
			END
		ELSE
			BEGIN
				SET @ERROR = 430
				RETURN
			END
	END
SET XACT_ABORT ON
BEGIN TRANSACTION
	DECLARE @PALLET_ID INT
	-- Kiểm tra xem pallet có đang trong kho đó không
	IF NOT EXISTS (
		SELECT A.STEP
		FROM (
		SELECT TOP(1) SOP.ID,SOP.STEP_NEXT_ID STEP
		FROM prod.[STEP_OF_PALLET] SOP
		LEFT JOIN prod.[PACKAGE] P ON P.STEP_OF_PALLET_ID = SOP.ID AND P.DESTINATION_ID = SOP.STEP_NEXT_ID
		LEFT JOIN prod.[PALLET] PL ON PL.ID = SOP.PALLET_ID
		WHERE PL.CODE = @PALLET_CODE
		ORDER BY SOP.ID DESC
		) A
		LEFT JOIN prod.PACKAGE P ON P.STEP_OF_PALLET_ID = A.ID
		WHERE P.VERIFY_BY IS NOT NULL
		AND A.STEP = @FROM_ID
	)BEGIN
		ROLLBACK
		SET @ERROR = 4505
		RETURN
	END

	SELECT @PALLET_ID = ID 
	FROM prod.[PALLET] 
	WHERE [CODE] = @PALLET_CODE

	DECLARE @STEP_OF_PALLET_ID INT
	DECLARE @STEP_OF_PALLET_GUID UNIQUEIDENTIFIER = NEWID()
	DECLARE @PACKAGE_ID INT
	DECLARE @PACKAGE_GUID UNIQUEIDENTIFIER = NEWID()
	DECLARE @ITEM_IN_PACKAGE_ID INT
	DECLARE @ITEM_IN_PACKAGE_GUID UNIQUEIDENTIFIER = NEWID()
	
	DECLARE @ITEM_COUNT INT

	SELECT @ITEM_COUNT = COUNT(ID)
	FROM prod.[ITEM_IN_PALLET]
	WHERE [PALLET_ID] = @PALLET_ID

	IF @ITEM_COUNT > 1 -- Chỉ là các pallet đạt 100%
		BEGIN
			INSERT INTO prod.[STEP_OF_PALLET]([GUID],PALLET_ID,STEP_ID,STEP_NEXT_ID,KILN_BATCH_ID,PLAN_ID,CREATE_BY,CREATE_DATE)
			VALUES(@STEP_OF_PALLET_GUID,@PALLET_ID,@FROM_ID,@TO_ID,@KILN_BATCH_ID,@PLAN_ID,@ACCOUNT_ID, GETDATE())
			SELECT @STEP_OF_PALLET_ID = ID FROM prod.[STEP_OF_PALLET] WHERE [GUID] = @STEP_OF_PALLET_GUID

			INSERT INTO prod.[PACKAGE]([GUID],[STEP_OF_PALLET_ID],SOURCE_ID,DESTINATION_ID,CREATE_BY,CREATE_DATE)
			VALUES(@PACKAGE_GUID,@STEP_OF_PALLET_ID,@FROM_ID,@TO_ID,@ACCOUNT_ID,GETDATE())
			SELECT @PACKAGE_ID = ID FROM prod.[PACKAGE] WHERE [GUID] = @PACKAGE_GUID

			INSERT INTO prod.[ITEM_IN_PACKAGE]([GUID],PACKAGE_ID,ITEM_ID,QUANTITY)
			SELECT NEWID(),@PACKAGE_ID,ITEM_ID,QUANTITY
			FROM prod.[ITEM_IN_PALLET]
			WHERE PALLET_ID = @PALLET_ID

			INSERT INTO prod.[MATERIALS_IN_PACKAGE]([GUID],[ITEM_IN_PACKAGE_ID],[ITEM_ID],QUANTITY)
			SELECT NEWID(),IIP.ID,IIP.ITEM_ID,IIP.QUANTITY 
			FROM prod.[ITEM_IN_PACKAGE] IIP
            WHERE IIP.PACKAGE_ID = @PACKAGE_ID
		END
	ELSE
		BEGIN
			DECLARE @ITEM_ID INT
			SELECT @ITEM_ID = ITEM_ID FROM prod.[ITEM_IN_PALLET] WHERE [PALLET_ID] = @PALLET_ID

			--Lấy tồn
			DECLARE @INVENTORY MONEY
            -- Lấy lần pass cuối cùng
			SELECT TOP(1) @INVENTORY = PASS
            FROM prod.[STEP_OF_PALLET]
            WHERE PALLET_ID = @PALLET_ID
			ORDER BY ID DESC
            -- Nếu chưa có lần pass nào thì sẽ thấy số lượng đầu tiên.
            IF(@INVENTORY IS NULL)
                BEGIN
                    SELECT @INVENTORY = [QUANTITY]
                    FROM prod.ITEM_IN_PALLET IIP
                    WHERE IIP.PALLET_ID = @PALLET_ID
					-- Nếu chưa có lần pass nào mà cũng ko có thông tin pass thì lấy số lượng đầu tiên
					IF (@PASS IS NULL)
						BEGIN
							SELECT @PASS = [QUANTITY]
							FROM prod.ITEM_IN_PALLET IIP
							WHERE IIP.PALLET_ID = @PALLET_ID
						END
                END
			ELSE
				BEGIN
					-- Nếu đã có lần pass mà lại ko có thông tin pass thì lấy lần pass cuối cùng
					IF(@PASS IS NULL)
						BEGIN
							SET @PASS = @INVENTORY
						END
				END

			DECLARE @NOT_PASS MONEY = (@INVENTORY - @PASS)

            IF(@NOT_PASS < 0) -- còn 200 không thể pass 300 được
            BEGIN
                ROLLBACK
                SET @ERROR = 4502
                RETURN
            END

			INSERT INTO prod.[STEP_OF_PALLET]([GUID],PALLET_ID,STEP_ID,STEP_NEXT_ID,ITEM_ID,PASS,NOT_PASS,KILN_BATCH_ID,PLAN_ID,CREATE_BY,CREATE_DATE)
			VALUES(@STEP_OF_PALLET_GUID,@PALLET_ID,@FROM_ID,@TO_ID,@ITEM_ID,@PASS,@NOT_PASS,@KILN_BATCH_ID,@PLAN_ID,@ACCOUNT_ID, GETDATE())
			SELECT @STEP_OF_PALLET_ID = ID FROM prod.[STEP_OF_PALLET] WHERE [GUID] = @STEP_OF_PALLET_GUID

			INSERT INTO prod.[PACKAGE]([GUID],[STEP_OF_PALLET_ID],SOURCE_ID,DESTINATION_ID,CREATE_BY,CREATE_DATE)
			VALUES(@PACKAGE_GUID,@STEP_OF_PALLET_ID,@FROM_ID,@TO_ID,@ACCOUNT_ID,GETDATE())
			SELECT @PACKAGE_ID = ID FROM prod.[PACKAGE] WHERE [GUID] = @PACKAGE_GUID

			INSERT INTO prod.[ITEM_IN_PACKAGE]([GUID],PACKAGE_ID,ITEM_ID,QUANTITY)
			VALUES (@ITEM_IN_PACKAGE_GUID,@PACKAGE_ID,@ITEM_ID,@PASS)
			SELECT @ITEM_IN_PACKAGE_ID = ID FROM prod.[ITEM_IN_PACKAGE] WHERE [GUID] = @ITEM_IN_PACKAGE_GUID

			INSERT INTO prod.[MATERIALS_IN_PACKAGE]([GUID],[ITEM_IN_PACKAGE_ID],[ITEM_ID],QUANTITY)
			VALUES (NEWID(),@ITEM_IN_PACKAGE_ID,@ITEM_ID,@PASS)
			
			IF (@NOT_PASS > 0) --  
			BEGIN
				DECLARE @ERROR_HANDLING INT
                -- Lấy nơi xuất hàng lỗi của bộ phận này.
				SELECT @ERROR_HANDLING = [ERROR]
				FROM base.DEPARTMENT
				WHERE ID = @FROM_ID
				
				IF @ERROR_HANDLING IS NULL
				BEGIN
					ROLLBACK
					SET @ERROR = 4540
					RETURN
				END

				INSERT INTO prod.[PACKAGE]([GUID],[STEP_OF_PALLET_ID],SOURCE_ID,DESTINATION_ID,CREATE_BY,CREATE_DATE)
				VALUES(@PACKAGE_GUID,@STEP_OF_PALLET_ID,@FROM_ID,@ERROR_HANDLING,@ACCOUNT_ID,GETDATE())
				SELECT @PACKAGE_ID = ID FROM prod.[PACKAGE] WHERE [GUID] = @PACKAGE_GUID

				INSERT INTO prod.[ITEM_IN_PACKAGE]([GUID],PACKAGE_ID,ITEM_ID,QUANTITY)
				VALUES (@ITEM_IN_PACKAGE_GUID,@PACKAGE_ID,@ITEM_ID,@NOT_PASS)
				SELECT @ITEM_IN_PACKAGE_ID = ID FROM prod.[ITEM_IN_PACKAGE] WHERE [GUID] = @ITEM_IN_PACKAGE_GUID

				INSERT INTO prod.[MATERIALS_IN_PACKAGE]([GUID],[ITEM_IN_PACKAGE_ID],[ITEM_ID],QUANTITY)
				VALUES (NEWID(),@ITEM_IN_PACKAGE_ID,@ITEM_ID,@NOT_PASS)
			END

		END
COMMIT


	SELECT ID
	FROM prod.[STEP_OF_PALLET]
	WHERE ID = @STEP_OF_PALLET_ID

    -- Trả về thông tin package
    -- có thể tra về 2 package vì nếu có hàng lỗi
    SELECT
    P.ID packageId,
    P.SOURCE_ID fromId,
    P.DESTINATION_ID toId,
    P.CREATE_BY createBy,
    P.CREATE_DATE createDate
    FROM prod.STEP_OF_PALLET SOP
    LEFT JOIN prod.PACKAGE P ON P.STEP_OF_PALLET_ID = SOP.ID
    WHERE SOP.ID = @STEP_OF_PALLET_ID

    -- Trả về thông tin item trong package
    SELECT
	P.ID packageId,
	IIP.ID itemInPackageId,
	IIP.ITEM_ID itemId,
	IIP.QUANTITY quantity
    FROM prod.STEP_OF_PALLET SOP
    LEFT JOIN prod.PACKAGE P ON P.STEP_OF_PALLET_ID = SOP.ID
    LEFT JOIN prod.ITEM_IN_PACKAGE IIP ON IIP.PACKAGE_ID = P.ID
    WHERE SOP.ID = @STEP_OF_PALLET_ID
    -- Ko cần trả về nguyên liệu của pallet vì sẽ bằng chính thành phẩm.



END