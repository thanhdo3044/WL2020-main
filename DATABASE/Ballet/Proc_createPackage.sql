CREATE OR ALTER PROC Proc_createPackage
@FROM_ID INT,
@TO_ID INT,
@ITEM_FROM_ID INT,
@ITEM_ID INT,
@QUANTITY MONEY,
@MATERIALS_ID INT,
@MATERIALS_QUANTITY MONEY,
@TYPE_ID INT,
@REMEDIES_ID INT,
@DESCRIPTION NVARCHAR(1000),
@PO NVARCHAR(50),
@ACCOUNT_ID INT,
@ERROR INT OUTPUT
AS
BEGIN
SET XACT_ABORT ON
BEGIN TRANSACTION
    DECLARE @PACKAGE_ID INT
    DECLARE @PACKAGE_GUID UNIQUEIDENTIFIER = NEWID()
    DECLARE @ITEM_IN_PACKAGE_ID INT
    DECLARE @ITEM_IN_PACKAGE_GUID UNIQUEIDENTIFIER = NEWID() 

    INSERT INTO prod.[PACKAGE]([GUID],SOURCE_ID,DESTINATION_ID,ITEM_FROM_ID,[TYPE_ID],REMEDIES_ID,[DESCRIPTION],CREATE_BY,CREATE_DATE, PO)
    VALUES(@PACKAGE_GUID,@FROM_ID,@TO_ID,@ITEM_FROM_ID,@TYPE_ID,@REMEDIES_ID,@DESCRIPTION,@ACCOUNT_ID,GETDATE(),@PO)
    SELECT @PACKAGE_ID = ID FROM prod.[PACKAGE] WHERE [GUID] = @PACKAGE_GUID

    INSERT INTO prod.[ITEM_IN_PACKAGE]([GUID],PACKAGE_ID,ITEM_ID,QUANTITY)
    VALUES(@ITEM_IN_PACKAGE_GUID,@PACKAGE_ID,@ITEM_ID,@QUANTITY)
    SELECT @ITEM_IN_PACKAGE_ID = ID FROM prod.[ITEM_IN_PACKAGE] WHERE [GUID] = @ITEM_IN_PACKAGE_GUID

	IF (@MATERIALS_ID IS NULL)
		BEGIN
			SET @MATERIALS_ID = @ITEM_ID
		END
	IF(@MATERIALS_QUANTITY IS NULL)
		BEGIN
			SET @MATERIALS_QUANTITY = @QUANTITY
		END

	IF EXISTS (
		select B.ID
		from prod.BOM B
		where B.ITEM_ID = @ITEM_ID
	)
		BEGIN
			IF EXISTS (
				SELECT R.ID
				from prod.ROUTING R
				where R.[ORDER] = 1
				and R.ITEM_ID = @ITEM_ID
				and R.STEP_ID = @FROM_ID
			) -- Tại công đoạn đầu tiên sẽ lấy theo định mức
				BEGIN
					INSERT INTO prod.MATERIALS_IN_PACKAGE(GUID,ITEM_IN_PACKAGE_ID,ITEM_ID,QUANTITY)
					SELECT NEWID(),@ITEM_IN_PACKAGE_ID,B.MATERIALS_ID, @QUANTITY * B.RATE
					FROM prod.BOM B
					WHERE B.ITEM_ID = @ITEM_ID
				END
			ELSE
				BEGIN
					INSERT INTO prod.[MATERIALS_IN_PACKAGE]([GUID],ITEM_IN_PACKAGE_ID,ITEM_ID,QUANTITY)
					VALUES(NEWID(),@ITEM_IN_PACKAGE_ID,@MATERIALS_ID,@MATERIALS_QUANTITY)
				END
		END
	ELSE
		BEGIN
			INSERT INTO prod.[MATERIALS_IN_PACKAGE]([GUID],ITEM_IN_PACKAGE_ID,ITEM_ID,QUANTITY)
			VALUES(NEWID(),@ITEM_IN_PACKAGE_ID,@MATERIALS_ID,@MATERIALS_QUANTITY)
		END
COMMIT

-- Trả về thông tin package
	SELECT
	P.ID id
	FROM prod.PACKAGE P
	WHERE P.[ID] = @PACKAGE_ID



-- Trả về thông tin package
	SELECT
	P.ID packageId,
	P.SOURCE_ID fromId,
	P.DESTINATION_ID toId,
	P.CREATE_BY createBy,
	P.CREATE_DATE createDate
	FROM prod.PACKAGE P
	WHERE P.[ID] = @PACKAGE_ID
	-- Trả về thông tin item trong package
	SELECT 
	P.ID packageId,
	IIP.ID itemInPackageId,
	IIP.ITEM_ID itemId,
	IIP.QUANTITY quantity
	FROM prod.PACKAGE P
	LEFT JOIN prod.ITEM_IN_PACKAGE IIP ON IIP.PACKAGE_ID = P.ID
	WHERE P.ID = @PACKAGE_ID
	-- Trả về nguyên liệu của pallet này
	SELECT 
	IIP.ID itemInPackageId,
	MIP.ID materialsInPackageId,
	MIP.ITEM_ID itemId,
	MIP.QUANTITY quantity
	FROM prod.PACKAGE P
	LEFT JOIN prod.ITEM_IN_PACKAGE IIP ON IIP.PACKAGE_ID = P.ID
	LEFT JOIN prod.MATERIALS_IN_PACKAGE MIP ON MIP.ITEM_IN_PACKAGE_ID = IIP.ID
	WHERE P.ID = @PACKAGE_ID
END